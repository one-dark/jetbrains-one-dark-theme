buildscript {
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module("com.overzealous:remark:1.1.0") using module("com.wavefront:remark:2023-07.07") because "not available on maven central anymore"
    }
  }
}

plugins {
    id 'one-dark-theme-plugin'
    id 'org.jetbrains.intellij' version '1.9.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'org.jlleitschuh.gradle.ktlint' version '8.2.0'
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'com.markskelton'
def projectVersion = System.getenv().getOrDefault('VERSION', '').replace('refs/tags/v', '')
version projectVersion

repositories {
    mavenCentral()
}

dependencies {
  implementation 'io.sentry:sentry:5.0.1'
  implementation 'commons-io:commons-io:2.6'
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version.set('2021.3.1')
  type.set('IU')
  downloadSources.set(true)
  updateSinceUntilBuild.set(true)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

markdownToHtml {
  sourceDir = new File("$projectDir/build/markdown")
  outputDir = new File("$projectDir/build/html")

  tables = false
}

tasks.runIde {
  def idePath = project.hasProperty("idePath") ? project.findProperty("idePath") : ""
  if(!"".equalsIgnoreCase(idePath)) {
    ideDir.set(file(idePath))
  }
}

tasks.runPluginVerifier {
  ideVersions.set([
    "IC-2020.3.1",
    "IC-2022.3",
  ])
}

tasks.patchPluginXml {
    version.set(projectVersion)
    sinceBuild.set('203.7148.57')
    untilBuild.set('242.*')

    def changelogPath = "$projectDir/build/html/CHANGELOG.html"
    def readmePath = "$projectDir/build/html/README.html"

    if (file(changelogPath).exists()) {
        changeNotes.set(provider {
          file(changelogPath).text
        })
    }

    if (file(readmePath).exists()) {
        pluginDescription.set(provider {
          file(readmePath).text
        })
    }
}

tasks.markdownToHtml.dependsOn("createReleaseNotes")
tasks.markdownToHtml.dependsOn("copyReadme")
tasks.patchReadMeHtml.dependsOn("markdownToHtml")
tasks.patchPluginXml.dependsOn("patchReadMeHtml")
tasks.patchPluginXml.dependsOn("createThemes")

publishPlugin {
    token.set(System.getenv('PUBLISH_TOKEN'))
}
