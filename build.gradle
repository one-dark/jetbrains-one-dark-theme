buildscript {
  repositories {
    mavenCentral()
  }
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      // Fix for missing dependency in Maven Central
      substitute module("com.overzealous:remark:1.1.0") using module("com.wavefront:remark:2023-07.07") because "not available on maven central anymore"
    }
  }
}

plugins {
  id 'one-dark-theme-plugin'
  id 'org.jetbrains.intellij' version '1.17.3'
  id 'org.jetbrains.kotlin.jvm' version '1.9.22'
  id 'org.jlleitschuh.gradle.ktlint' version '11.5.1'
  id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'com.markskelton'
def projectVersion = System.getenv().getOrDefault('VERSION', '').replace('refs/tags/v', '')
version projectVersion

repositories {
  mavenCentral()
}

dependencies {
  implementation 'io.sentry:sentry:5.0.1'
  implementation 'commons-io:commons-io:2.6'
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version.set('2024.1.4')
  type.set('IC')
  downloadSources.set(true)
  updateSinceUntilBuild.set(true)
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileKotlin {
  kotlinOptions.jvmTarget = "17"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "17"
}

markdownToHtml {
  sourceDir = new File("$projectDir/build/markdown")
  outputDir = new File("$projectDir/build/html")
}

tasks.runIde {
  // Give the IDE enough memory
  jvmArgs = ["-Xmx2g"]

  def idePath = project.hasProperty("idePath") ? project.findProperty("idePath") : ""
  if(!"".equalsIgnoreCase(idePath)) {
    ideDir.set(file(idePath))
  }
}

tasks.runPluginVerifier {
  ideVersions.set([
    "IC-2024.1",
    "IC-2023.3"
  ])
}

tasks.patchPluginXml {
  version.set(projectVersion)
  sinceBuild.set('241')
  untilBuild = provider { null }

  def changelogPath = "$projectDir/build/html/CHANGELOG.html"
  def readmePath = "$projectDir/build/html/README.html"

  if (file(changelogPath).exists()) {
    changeNotes.set(provider {
      file(changelogPath).text
    })
  }

  if (file(readmePath).exists()) {
    pluginDescription.set(provider {
      file(readmePath).text
    })
  }
}

tasks.markdownToHtml.dependsOn("createReleaseNotes")
tasks.markdownToHtml.dependsOn("copyReadme")
tasks.patchReadMeHtml.dependsOn("markdownToHtml")
tasks.patchPluginXml.dependsOn("patchReadMeHtml")
tasks.patchPluginXml.dependsOn("createThemes")

publishPlugin {
  token.set(System.getenv('PUBLISH_TOKEN'))
}
