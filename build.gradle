plugins {
    id 'org.jetbrains.intellij' version '0.4.7'
}

// Gets the version name from the latest Git tag
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }

    stdout.toString().trim().substring(1)
}

group 'com.markskelton'
version getVersionName()

repositories {
    mavenCentral()
}

intellij {
    version 'LATEST-EAP-SNAPSHOT'
}

def getSecret = { prop, env ->
    project.hasProperty(prop) ? project.findProperty(prop) : System.getenv(env)
}

publishPlugin {
    username getSecret('publishUsername', 'PUBLISH_USERNAME')
    token getSecret('publishToken', 'PUBLISH_TOKEN')
}
